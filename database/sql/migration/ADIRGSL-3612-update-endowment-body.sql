BEGIN TRY
BEGIN TRAN

DECLARE @CurrentValues TABLE
(
   BENEFICIARY_CODE NVARCHAR(50),
   BENFICIARY_INDEX NVARCHAR(50),
   ENDOWMENT_NUMBER NVARCHAR(255),
   PAYMENT_ORDER_NUMBER NVARCHAR(255),
   PAYMENT_ORDER_SUBTYPE  NVARCHAR(255)
);

INSERT INTO @CurrentValues

select bn.BENEFICIARY_CODE,
	   b.[key] AS BENFICIARY_INDEX,
	   doc.UNIVERSAL_DOCUMENT_NUMBER AS ENDOWMENT_NUMBER,
	   poHub.PAYMENT_ORDER_NUMBER,
	   poSat.PAYMENT_ORDER_SUBTYPE
from BFX.UNIVERSAL_DOCUMENT doc
JOIN CFX.PUBLISHED_ARTIFACT art ON art.PUBLISHED_ARTIFACT_ID = doc.PUBLISHED_ARTIFACT_ID
INNER JOIN ACC_IMPL.PAYMENT_ORDER_SAT_LATEST poSat on poSat.REFERENCE_NUMBER = doc.UNIVERSAL_DOCUMENT_NUMBER
INNER JOIN ACC_IMPL.PAYMENT_ORDER_HUB poHub on poHub.PAYMENT_ORDER_HKEY = poSat.PAYMENT_ORDER_HKEY
INNER JOIN ACC.PAYMENT_ORDER po on po.PAYMENT_ORDER_NUMBER = poHub.PAYMENT_ORDER_NUMBER
INNER JOIN CFG.PROCESS_STATE st on st.PROCESS_STATE_ID = po.STATE_ID
INNER JOIN ACC_IMPL.PO_RECIPIENT_LINK rLink on rLink.PAYMENT_ORDER_HKEY = poHub.PAYMENT_ORDER_HKEY
INNER JOIN PTY_IMPL.PARTY_HUB pHub on pHub.PARTY_HKEY = rLink.PARTY_HKEY
CROSS APPLY OPENJSON(doc.BODY, '$.endowmentBeneficiaries') b
CROSS APPLY OPENJSON(b.[value]) WITH (BENEFICIARY_CODE NVARCHAR(64) '$.partyCode') bn
WHERE 
art.CODE_NAME = 'Endowment'
AND st.CODE_NAME <> 'Cancelled'
AND bn.BENEFICIARY_CODE = pHub.PARTY_CODE
GROUP BY bn.BENEFICIARY_CODE, b.[key], doc.COMMON_BODY, doc.BODY, doc.UNIVERSAL_DOCUMENT_NUMBER, poHub.PAYMENT_ORDER_NUMBER, poSat.PAYMENT_ORDER_SUBTYPE

DECLARE @BeneficiaryCode NVARCHAR(50), @BeneficiaryIndex NVARCHAR(50), @EndowmentNumber NVARCHAR(255), @PaymentOrderNumber NVARCHAR(255), @PaymentOrderSubtype NVARCHAR(255)

DECLARE EWT_CURSOR CURSOR LOCAL READ_ONLY FORWARD_ONLY
FOR 

SELECT BENEFICIARY_CODE,
	   BENFICIARY_INDEX,
	   ENDOWMENT_NUMBER,
	   PAYMENT_ORDER_NUMBER,
	   PAYMENT_ORDER_SUBTYPE
FROM @CurrentValues

OPEN EWT_CURSOR
FETCH NEXT FROM EWT_CURSOR INTO @BeneficiaryCode, @BeneficiaryIndex, @EndowmentNumber, @PaymentOrderNumber, @PaymentOrderSubtype
WHILE @@FETCH_STATUS = 0

BEGIN 

	IF @PaymentOrderSubtype = 'Endowment'
	BEGIN

	UPDATE BFX.UNIVERSAL_DOCUMENT SET BODY = JSON_MODIFY(BODY, '$.endowmentBeneficiaries[' + @BeneficiaryIndex + '].assignedPaymentOrderNumber', @PaymentOrderNumber),
	COMMON_BODY = JSON_MODIFY(COMMON_BODY, '$.attributes.endowmentBeneficiaries[' + @BeneficiaryIndex + '].assignedPaymentOrderNumber', @PaymentOrderNumber) 
	WHERE UNIVERSAL_DOCUMENT_NUMBER = @EndowmentNumber

	UPDATE BFX.UNIVERSAL_DOCUMENT SET SYS_UPDATED_ON = SYSDATETIME() WHERE UNIVERSAL_DOCUMENT_NUMBER = @EndowmentNumber

	UPDATE bSat SET bSat.ASSIGNED_PO_NUMBER = @PaymentOrderNumber
	FROM EWT_IMPL.EWT_HUB eHub
	INNER JOIN EWT_IMPL.EWT_BENEFICIARY_LINK bLink on bLink.EWT_HKEY = eHub.EWT_HKEY
	INNER JOIN PTY_IMPL.PARTY_HUB pHub on pHub.PARTY_HKEY = bLink.PARTY_HKEY
	INNER JOIN EWT_IMPL.EWT_BENEFICIARY_SAT_LATEST bSat on bSat.EWT_BENEFICIARY_HKEY = bLink.EWT_BENEFICIARY_HKEY
	WHERE eHub.ENDOWMENT_NUMBER = @EndowmentNumber and pHub.PARTY_CODE = @BeneficiaryCode and bSat.IS_DELETED = 0

	END

	ELSE IF @PaymentOrderSubtype = 'EndowmentPIT'
	BEGIN

	UPDATE BFX.UNIVERSAL_DOCUMENT SET BODY = JSON_MODIFY(BODY, '$.endowmentBeneficiaries[' + @BeneficiaryIndex + '].assignedPitPaymentOrderNumber', @PaymentOrderNumber),
	COMMON_BODY = JSON_MODIFY(COMMON_BODY, '$.attributes.endowmentBeneficiaries[' + @BeneficiaryIndex + '].assignedPitPaymentOrderNumber', @PaymentOrderNumber) 
	WHERE UNIVERSAL_DOCUMENT_NUMBER = @EndowmentNumber

	UPDATE BFX.UNIVERSAL_DOCUMENT SET SYS_UPDATED_ON = SYSDATETIME() WHERE UNIVERSAL_DOCUMENT_NUMBER = @EndowmentNumber

	UPDATE bSat SET bSat.ASSIGNED_PIT_PO_NUMBER = @PaymentOrderNumber
	FROM EWT_IMPL.EWT_HUB eHub
	INNER JOIN EWT_IMPL.EWT_BENEFICIARY_LINK bLink on bLink.EWT_HKEY = eHub.EWT_HKEY
	INNER JOIN PTY_IMPL.PARTY_HUB pHub on pHub.PARTY_HKEY = bLink.PARTY_HKEY
	INNER JOIN EWT_IMPL.EWT_BENEFICIARY_SAT_LATEST bSat on bSat.EWT_BENEFICIARY_HKEY = bLink.EWT_BENEFICIARY_HKEY
	WHERE eHub.ENDOWMENT_NUMBER = @EndowmentNumber and pHub.PARTY_CODE = @BeneficiaryCode and bSat.IS_DELETED = 0

	END
	
	FETCH NEXT FROM EWT_CURSOR INTO @BeneficiaryCode, @BeneficiaryIndex, @EndowmentNumber, @PaymentOrderNumber, @PaymentOrderSubtype

END

CLOSE EWT_CURSOR
DEALLOCATE EWT_CURSOR

COMMIT TRAN

END TRY

BEGIN CATCH

    ROLLBACK TRAN
    DECLARE @ErrorMessage NVARCHAR(4000); DECLARE @ErrorSeverity INT; DECLARE @ErrorState INT;
    SELECT @ErrorMessage = ERROR_MESSAGE(), @ErrorSeverity = ERROR_SEVERITY(), @ErrorState = 1;
    RAISERROR (@ErrorMessage, @ErrorSeverity, @ErrorState);

END CATCH