BEGIN TRY
BEGIN TRAN

DECLARE @CurrentSPValues TABLE
(
   AGENT_AGREEMENT_NUMBER NVARCHAR(50),
   SP_CODE NVARCHAR(50),
   PARTNER_CODE NVARCHAR(50)
);

INSERT INTO @CurrentSPValues

SELECT aaQ.AGENT_AGREEMENT_NUMBER,
	   aaQ.SP_CODE,
	   spSat.PARTNER_CODE
FROM
(SELECT AGENT_AGREEMENT_NUMBER,
	    JSON_VALUE(aa.BODY, '$.participants.agent.serviceProviderCode') AS SP_CODE
 FROM pas.AGENT_AGREEMENT aa) aaQ
JOIN ORG_IMPL.SERVICE_PROVIDER_HUB spHub on spHub.SERVICE_PROVIDER_CODE = aaQ.SP_CODE
JOIN ORG_IMPL.SERVICE_PROVIDER_INFO_SAT spSat on spSat.SERVICE_PROVIDER_INFO_HKEY = spHub.SERVICE_PROVIDER_HKEY

DECLARE @AaNumber NVARCHAR(50), @SpCode NVARCHAR(50), @PartnerCode NVARCHAR(50)

DECLARE AA_CURSOR CURSOR LOCAL READ_ONLY FORWARD_ONLY
FOR 

SELECT AGENT_AGREEMENT_NUMBER,
	   SP_CODE,
	   PARTNER_CODE
FROM @CurrentSPValues

OPEN AA_CURSOR
FETCH NEXT FROM AA_CURSOR INTO @AaNumber, @SpCode, @PartnerCode
WHILE @@FETCH_STATUS = 0

BEGIN 

	UPDATE pas.AGENT_AGREEMENT set BODY = JSON_MODIFY(BODY, '$.participants.agent.businessCode', @PartnerCode) WHERE AGENT_AGREEMENT_NUMBER = @AaNumber
	UPDATE pas.AGENT_AGREEMENT set COMMON_BODY = JSON_MODIFY(COMMON_BODY, '$.attributes.participants.agent.businessCode', @PartnerCode) WHERE AGENT_AGREEMENT_NUMBER = @AaNumber
	UPDATE pas.AGENT_AGREEMENT set SNAPSHOT_BODY = JSON_MODIFY(SNAPSHOT_BODY, '$.participants.agent.businessCode', @PartnerCode) WHERE AGENT_AGREEMENT_NUMBER = @AaNumber
	
	FETCH NEXT FROM AA_CURSOR INTO @AaNumber, @SpCode, @PartnerCode

END

CLOSE AA_CURSOR
DEALLOCATE AA_CURSOR

COMMIT TRAN

END TRY

BEGIN CATCH

    ROLLBACK TRAN
    DECLARE @ErrorMessage NVARCHAR(4000); DECLARE @ErrorSeverity INT; DECLARE @ErrorState INT;
    SELECT @ErrorMessage = ERROR_MESSAGE(), @ErrorSeverity = ERROR_SEVERITY(), @ErrorState = 1;
    RAISERROR (@ErrorMessage, @ErrorSeverity, @ErrorState);

END CATCH