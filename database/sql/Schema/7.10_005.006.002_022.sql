-- replace CURRENCY_CODE column with two new: PAY_CURRENCY_CODE and DOC_CURRENCY_CODE
EXEC sp_rename N'ACC_IMPL.ALLOCATION.CURRENCY_CODE', 'PAY_CURRENCY_CODE', 'COLUMN'
GO
ALTER TABLE ACC_IMPL.ALLOCATION ADD DOC_CURRENCY_CODE nvarchar(3) NULL
GO
UPDATE ACC_IMPL.ALLOCATION SET DOC_CURRENCY_CODE = 'RUB'
GO
ALTER TABLE ACC_IMPL.ALLOCATION ALTER COLUMN DOC_CURRENCY_CODE nvarchar(3) NOT NULL
GO

-- add unique constraint on payment NO
ALTER TABLE ACC_IMPL.BANK_STATEMENT_ITEM
ADD CONSTRAINT UQ_BANK_STATEMENT_ITEM_NO UNIQUE (BANK_STATEMENT_ITEM_NO)
GO

-- change non-clustered index to clustered
-- -- drop references
alter table ACC_IMPL.MATCHING
drop constraint FK_ACC_IMPL_MATCHING_ALLOCATION_ID
go
alter table ACC_IMPL.MATCHING
drop constraint FK_ACC_IMPL_MATCHING_CANCELLED_MATCHING_ID
go
alter table ACC_IMPL.ALLOCATION
drop constraint FK_ACC_IMPL_ALLOCATION_CANCELLED_ALLOCATION_ID
go
-- -- drop indexes
alter table ACC_IMPL.ALLOCATION
drop constraint PK_ACC_IMPL_ALLOCATION_ALLOCATION_ID
GO
alter table ACC_IMPL.MATCHING
drop constraint PK_ACC_IMPL_MATCHING_MATCHING_ID
GO
-- -- create clustered indexes
alter table ACC_IMPL.MATCHING
add constraint [PK_ACC_IMPL_MATCHING_MATCHING_ID] PRIMARY KEY CLUSTERED
(
	[MATCHING_ID] ASC
)
GO
alter table ACC_IMPL.ALLOCATION
add constraint [PK_ACC_IMPL_ALLOCATION_ALLOCATION_ID] PRIMARY KEY CLUSTERED
(
	[ALLOCATION_ID] ASC
)
GO
-- -- create references
ALTER TABLE [ACC_IMPL].[ALLOCATION]  WITH CHECK ADD  CONSTRAINT [FK_ACC_IMPL_ALLOCATION_CANCELLED_ALLOCATION_ID] FOREIGN KEY([CANCELLED_ALLOCATION_ID])
REFERENCES [ACC_IMPL].[ALLOCATION] ([ALLOCATION_ID])
GO
ALTER TABLE [ACC_IMPL].[MATCHING]  WITH CHECK ADD  CONSTRAINT [FK_ACC_IMPL_MATCHING_ALLOCATION_ID] FOREIGN KEY([ALLOCATION_ID])
REFERENCES [ACC_IMPL].[ALLOCATION] ([ALLOCATION_ID])
GO
ALTER TABLE [ACC_IMPL].[MATCHING]  WITH CHECK ADD  CONSTRAINT [FK_ACC_IMPL_MATCHING_CANCELLED_MATCHING_ID] FOREIGN KEY([CANCELLED_MATCHING_ID])
REFERENCES [ACC_IMPL].[MATCHING] ([MATCHING_ID])
GO

-- delete allocations that do not have a referenced payment
BEGIN
	SELECT alc.allocation_id INTO #NO_BSI_ALC
	FROM acc_impl.ALLOCATION alc
	where alc.BANK_STATEMENT_ITEM_ID not in (select BANK_STATEMENT_ITEM_ID from acc_impl.BANK_STATEMENT_ITEM);
	delete from acc_impl.MATCHING where allocation_id in (select * from #NO_BSI_ALC);
	delete from acc_impl.ALLOCATION where allocation_id in (select * from #NO_BSI_ALC);
END;
GO

-- add BSI reference for Allocation table
ALTER TABLE [ACC_IMPL].[ALLOCATION]  WITH CHECK ADD  CONSTRAINT [FK_ACC_IMPL_ALLOCATION_BSI_ID] FOREIGN KEY([BANK_STATEMENT_ITEM_ID])
REFERENCES [ACC_IMPL].[BANK_STATEMENT_ITEM] ([BANK_STATEMENT_ITEM_ID])
GO

-- validate references
ALTER TABLE [ACC_IMPL].[ALLOCATION] CHECK CONSTRAINT [FK_ACC_IMPL_ALLOCATION_CANCELLED_ALLOCATION_ID]
GO
ALTER TABLE [ACC_IMPL].[ALLOCATION] CHECK CONSTRAINT [FK_ACC_IMPL_ALLOCATION_BSI_ID]
GO
ALTER TABLE [ACC_IMPL].[MATCHING] CHECK CONSTRAINT [FK_ACC_IMPL_MATCHING_ALLOCATION_ID]
GO
ALTER TABLE [ACC_IMPL].[MATCHING] CHECK CONSTRAINT [FK_ACC_IMPL_MATCHING_CANCELLED_MATCHING_ID]
GO
